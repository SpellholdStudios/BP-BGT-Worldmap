////////////////////////////////////////////
//Standard SoA and ToB areas and links
////////////////////////////////////////////
COPY "bp-bgt-worldmap/2da/revised_soa_tob_areas.2da" "bp-bgt-worldmap/2da/revised_soa_tob_areas.2da"
  LPM read_area_array_from_2da
BUT_ONLY

COPY "bp-bgt-worldmap/2da/revised_soa_tob_links.2da" "bp-bgt-worldmap/2da/revised_soa_tob_links.2da"
  LPM read_link_array_from_2da
BUT_ONLY


////////////////////////////////////////////
//Mod-added areas
////////////////////////////////////////////
//Add areas from "worldmap/map_mods_areas.tbl"
ACTION_IF FILE_EXISTS "worldmap/map_mods_areas.tbl" BEGIN
  COPY "worldmap/map_mods_areas.tbl" "worldmap/map_mods_areas.tbl"
    LPF fix_table END
    LPM read_area_array_from_2da
  BUT_ONLY
END


////////////////////////////////////////////
//Revised data for mod-added areas
////////////////////////////////////////////
COPY "bp-bgt-worldmap/revised/tbl/new_map_mods_areas.tbl" "bp-bgt-worldmap/revised/tbl/new_map_mods_areas.tbl"
  LPM read_area_array_from_2da
BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY mod_areas BEGIN
  AR6526 => BGT
  AR4220 => SOS
  DD3300 => TDD
  AR3610 => CtB
  RR3100 => RoT
  DSC001 => DSotSC
  AR01PB => NTotSC
  BH0100 => SoBH
  G3G12A => G3
  L#ND01 => SE // Southern Edge
END

ACTION_PHP_EACH mod_areas AS area => table BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%area%.are" BEGIN
    COPY "bp-bgt-worldmap/revised/tbl/new_map_mods_areas_%table%.tbl" "bp-bgt-worldmap/revised/tbl/new_map_mods_areas_%table%.tbl"
      LPM read_area_array_from_2da
    BUT_ONLY
  END
END


////////////////////////////////////////////
//Mod-added links
////////////////////////////////////////////

//This needs to be done before we read from revised/tbl/, so that the info in revised/tbl/ can override the info from worldmap/ when there's overlap

ACTION_DEFINE_ASSOCIATIVE_ARRAY mod_blacklists BEGIN
  AR6526 => BGT
  AR3610 => CtB
  AR01PB => NTotSC
  RR3100 => RoT
  AR4220 => SOS
  DD3300 => TDD
END

//Don't add links read from these tables when we add links from worldmap/
ACTION_PHP_EACH mod_blacklists AS area => mod BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%area%.are" BEGIN
    COPY "bp-bgt-worldmap/revised/tbl/blacklist_%mod%.tbl" "bp-bgt-worldmap/revised/tbl/blacklist_%mod%.tbl"
      LPM read_blacklist_array_from_2da
    BUT_ONLY
  END
END

ACTION_IF FILE_EXISTS "worldmap/map_mods_links.tbl" BEGIN
  COPY "worldmap/map_mods_links.tbl" "worldmap/map_mods_links.tbl"
    LPF fix_table END
    LPM read_link_array_from_2da
  BUT_ONLY
END

//Deactivate the blacklist, so it doesn't interfere when we read links from revised/tbl/
ACTION_PHP_EACH fl#LINK_BLACKLIST AS i => int BEGIN
  ACTION_IF int = 1 BEGIN
    OUTER_SET $fl#LINK_BLACKLIST("%i_0%" "%i_1%" "%i_2%") = 0
  END
END


////////////////////////////////////////////
//Revised data for mod-added links
////////////////////////////////////////////
COPY "bp-bgt-worldmap/revised/tbl/new_map_mods_links.tbl" "bp-bgt-worldmap/revised/tbl/new_map_mods_links.tbl"
  LPM read_link_array_from_2da
BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY mod_links BEGIN
  AR6526 => BGT
  AR4220 => SOS
  DD3300 => TDD
  AR3610 => CtB
  RR3100 => RoT
  DSC001 => DSotSC
  AR01PB => NTotSC
  BH0100 => SoBH
  G3G12A => G3
END

ACTION_PHP_EACH mod_links AS area => table BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%area%.are" BEGIN
    COPY "bp-bgt-worldmap/revised/tbl/new_map_mods_links_%table%.tbl" "bp-bgt-worldmap/revised/tbl/new_map_mods_links_%table%.tbl"
      LPM read_link_array_from_2da
    BUT_ONLY
  END
END


/* //The link-patch system is poorly designed and barely used. The only case seems to be CtB, but it uses it to overwrite vanilla Athkatla encounters
////////////////////////////////////////////
//Link patches
////////////////////////////////////////////
ACTION_IF FILE_EXISTS "worldmap/map_mods_links_patch.tbl" BEGIN
  COPY "worldmap/map_mods_links_patch.tbl" "worldmap/map_mods_links_patch.tbl"
    LPM apply_link_patches
  BUT_ONLY
END
*/


////////////////////////////////////////////
//Translations
////////////////////////////////////////////
ACTION_IF FILE_EXISTS "worldmap/map_mods_trans.tra" BEGIN
  COPY "worldmap/map_mods_trans.tra" "worldmap/map_mods_trans.tra"
    LPF fix_table END
    LPM read_trans_array_from_2da
  BUT_ONLY
END

COPY "bp-bgt-worldmap/language/english/worldmap.tra" "bp-bgt-worldmap/language/english/worldmap.tra" //To guard against incomplete translations
     "bp-bgt-worldmap/language/%LANGUAGE%/worldmap.tra" "bp-bgt-worldmap/language/%LANGUAGE%/worldmap.tra"
  LPM read_trans_array_from_2da
BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY mod_trans BEGIN
  AR6526 => BGT
  AR4220 => SOS
  DD3300 => TDD
  AR3610 => CtB
  RR3100 => RoT
  DSC001 => DSotSC
  AR01PB => NTotSC
  BH0100 => SoBH
  F_0115 => DS
  G3G12A => G3
END

ACTION_PHP_EACH mod_trans AS area => table BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%area%.are" BEGIN
    COPY "bp-bgt-worldmap/language/english/revised/%table%_new_map_mods_trans.tra"
         "bp-bgt-worldmap/language/english/revised/%table%_new_map_mods_trans.tra" //To guard against incomplete translations

         "bp-bgt-worldmap/language/%LANGUAGE%/revised/%table%_new_map_mods_trans.tra"
         "bp-bgt-worldmap/language/%LANGUAGE%/revised/%table%_new_map_mods_trans.tra"
      LPM read_trans_array_from_2da
    BUT_ONLY
  END
END

LAM merge_in_translations


////////////////////////////////////////////
//Code specific to "revised"
////////////////////////////////////////////
ACTION_IF FILE_EXISTS_IN_GAME AR6526.are BEGIN //BGT only
  EXTEND_TOP ar6704.bcs "bp-bgt-worldmap/baf/bgt/ar6704.baf"
  EXTEND_TOP ar6801.bcs "bp-bgt-worldmap/baf/bgt/ar6801.baf"
  EXTEND_TOP ar7000.bcs "bp-bgt-worldmap/baf/bgt/ar7000.baf"
  EXTEND_TOP ar8900.bcs "bp-bgt-worldmap/baf/bgt/ar8900.baf"
  EXTEND_TOP ar9000.bcs "bp-bgt-worldmap/baf/bgt/ar9000.baf"

  COPY_EXISTING tranzig.bcs override
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~IF[%LNL% %TAB%]+Die()[%LNL% %TAB%]+THEN[%LNL% %TAB%]+RESPONSE +#100[%LNL% %TAB%]+RevealAreaOnMap("AR8700")[%LNL% %TAB%]+END~ ~~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~IF[%LNL% %TAB%]+HPLT(Myself,20)[%LNL% %TAB%]+THEN[%LNL% %TAB%]+RESPONSE +#100[%LNL% %TAB%]+Dialogue([PC])[%LNL% %TAB%]+END~ ~~
    COMPILE_BAF_TO_BCS
  BUT_ONLY

  COPY_EXISTING ar6752.bcs override
    REPLACE_BCS_BLOCK "bp-bgt-worldmap/baf/bgt/oar6752.baf" "bp-bgt-worldmap/baf/bgt/nar6752.baf"
  BUT_ONLY

  COPY_EXISTING ar8701.bcs override
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~PartyHasItem("scrl2y")~ ~PartyHasItem("scrl2z")~
    COMPILE_BAF_TO_BCS
  BUT_ONLY

  COMPILE "bp-bgt-worldmap/d/imoen.d" USING
          "%tra_path%/english/revised/imoen.tra"
          "%tra_path%/%LANGUAGE%/revised/imoen.tra"

  COPY "bp-bgt-worldmap/bmp/bgt/ar8400sr.bmp" override
       "bp-bgt-worldmap/bmp/bgt/arw000sr.bmp" override
       "bp-bgt-worldmap/bmp/bgt/arw500sr.bmp" override

  //Removing Travel Triggers from ARW000 & ARW500 to enable WM6 travel on Werewolf Isle
  COPY_EXISTING ~ARW000.ARE~ ~OVERRIDE~
    SPRINT TRIGGERNAME "DoorNorthA"
    SET TRIGGERTYPE = 2
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "DoorNorthB"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "DoorNorthC"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "DoorNorthD"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "DoorNorthE"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "DoorNorthF"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "DoorNorthG"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  BUT_ONLY_IF_IT_CHANGES//*************************************************ARW000.ARE

  COPY_EXISTING ~ARW500.ARE~ ~OVERRIDE~
    SPRINT TRIGGERNAME "TravelSouth"
    SET TRIGGERTYPE = 2
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TravelSouth2"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TravelSouth3"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TravelSouth4"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TravelSouth5"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TravelSouth6"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TravelSouth7"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  BUT_ONLY_IF_IT_CHANGES//*************************************************ARW500.ARE
END

// Proper Area Visibility for NTotSC
ACTION_IF FILE_EXISTS_IN_GAME ar01pb.are BEGIN
  COPY "bp-bgt-worldmap/bmp/ntotsc/ar01pbsr.bmp" override
END

// Fix the Riatavin Plague Timer Issue
ACTION_IF FILE_EXISTS_IN_GAME dd3300.bcs BEGIN
  COPY_EXISTING dd3300.bcs override
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~SetGlobalTimer("RiatavinPlague","GLOBAL",TWO_DAYS)~ ~SetGlobalTimer("RiatavinPlague","GLOBAL",THREE_DAYS)~
    COMPILE_BAF_TO_BCS
  BUT_ONLY
END

//Removing Travel Triggers from Icewind Dale to enable WM6 travel for Region of Terror
ACTION_IF FILE_EXISTS_IN_GAME ra5100.are BEGIN
  COPY_EXISTING ~RA5100.ARE~ ~OVERRIDE~
    SPRINT TRIGGERNAME "TRAN5400"
    SET TRIGGERTYPE = 2
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  BUT_ONLY_IF_IT_CHANGES//*************************************************RA5100.ARE

  COPY_EXISTING ~RA5400.ARE~ ~OVERRIDE~
    SPRINT TRIGGERNAME "TRAN5100"
    SET TRIGGERTYPE = 2
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TRAN5401"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TRAN5600"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  BUT_ONLY_IF_IT_CHANGES//*************************************************RA5400.ARE

  COPY_EXISTING ~RA5401.ARE~ ~OVERRIDE~
    SPRINT TRIGGERNAME "TRAN5400"
    SET TRIGGERTYPE = 2
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  BUT_ONLY_IF_IT_CHANGES//*************************************************RA5401.ARE

  COPY_EXISTING ~RA5600.ARE~ ~OVERRIDE~
    SPRINT TRIGGERNAME "TRAN5400"
    SET TRIGGERTYPE = 2
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
    SPRINT TRIGGERNAME "TRAN5601"
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  BUT_ONLY_IF_IT_CHANGES//*************************************************RA5600.ARE

  COPY_EXISTING ~RA5601.ARE~ ~OVERRIDE~
    SPRINT TRIGGERNAME "TRAN5600"
    SET TRIGGERTYPE = 2
    LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  BUT_ONLY_IF_IT_CHANGES//*************************************************RA5601.ARE

  COPY "bp-bgt-worldmap/bmp/rot/ra5100sr.bmp" override
       "bp-bgt-worldmap/bmp/rot/ra5400sr.bmp" override
       "bp-bgt-worldmap/bmp/rot/ra5600sr.bmp" override
       "bp-bgt-worldmap/bmp/rot/ra5601sr.bmp" override
END
