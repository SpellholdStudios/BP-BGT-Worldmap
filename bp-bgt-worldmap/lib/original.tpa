ACTION_IF GAME_IS ~tob bgt~ BEGIN
  OUTER_SPRINT fl#area_table ~bp-bgt-worldmap/2da/original_soa_tob_areas.2da~
  OUTER_SPRINT fl#link_table ~bp-bgt-worldmap/2da/original_soa_tob_links.2da~
END ELSE ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
  OUTER_SPRINT fl#area_table ~bp-bgt-worldmap/2da/original_tutu_areas.2da~
  OUTER_SPRINT fl#link_table ~bp-bgt-worldmap/2da/original_tutu_links.2da~
END ELSE ACTION_IF GAME_IS ~bgee~ BEGIN
  OUTER_SPRINT fl#area_table ~bp-bgt-worldmap/2da/original_bg1ee_areas.2da~
  OUTER_SPRINT fl#link_table ~bp-bgt-worldmap/2da/original_bg1ee_links.2da~
END ELSE ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
  OUTER_SPRINT fl#area_table ~bp-bgt-worldmap/2da/original_bg2ee_areas.2da~
  OUTER_SPRINT fl#link_table ~bp-bgt-worldmap/2da/original_bg2ee_links.2da~
END

////////////////////////////////////////////
//Standard areas and links
////////////////////////////////////////////
COPY ~%fl#area_table%~ ~%fl#area_table%~
  LPM read_area_array_from_2da
BUT_ONLY

COPY ~%fl#link_table%~ ~%fl#link_table%~
  LPM read_link_array_from_2da
BUT_ONLY


////////////////////////////////////////////
//Mod-added areas
////////////////////////////////////////////
ACTION_IF FILE_EXISTS "worldmap/map_mods_areas.tbl" BEGIN
  COPY "worldmap/map_mods_areas.tbl" "worldmap/map_mods_areas.tbl"
    LPF fix_table END
    LPM read_area_array_from_2da
  BUT_ONLY
END

//"Update" the area info with stuff we provide
//This really should be done as patches insead of overwriting all the area info
ACTION_DEFINE_ASSOCIATIVE_ARRAY mod_tables BEGIN
  BG0300 => eet
  YS0360 => fft
  ACIL01 => Silverrealms
  AR6526 => BGT
  AR4220 => SOS
  DD3300 => TDD
  AR3610 => CtB
  RR3100 => RoT
  DSC001 => DSotSC
  AR01PB => NTotSC
  BH0100 => SoBH
  G3G12A => G3Anniversary
  F_7777 => DS

  // Patches for below mods can be removed after an update of the corresponding mod with correct map icons and positions
  ARLDV1 => TotDG // Tales of the Deep Gardens
  ARISH1 => Innershade
  ARES01 => Eilistraee
  WQ0001 => WhiteQueen
  L#FOU0 => Foundling
  L#ND01 => SouthernEdge
  L#NI01 => TangledOakIsle
  L#AF01 => WhinHill
  L#ORM1 => SotSC // Shades of the Sword Coast
  L#XZE1 => InLoveUndeath
END

ACTION_PHP_EACH mod_tables AS area => table BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%area%.are" BEGIN
    COPY "bp-bgt-worldmap/original/tbl/updates/%table%_areas.tbl" "bp-bgt-worldmap/original/tbl/updates/%table%_areas.tbl"
      LPM read_area_array_from_2da
    BUT_ONLY
  END
END

////////////////////////////////////////////
//Mod-added links
////////////////////////////////////////////
ACTION_IF FILE_EXISTS "worldmap/map_mods_links.tbl" BEGIN
  COPY "worldmap/map_mods_links.tbl" "worldmap/map_mods_links.tbl"
    LPF fix_table END
    LPM read_link_array_from_2da
  BUT_ONLY
END


/* //The link-patch system is poorly designed and barely used. The only case seems to be CtB, but it uses it to overwrite vanilla Athkatla encounters
////////////////////////////////////////////
//Link patches
////////////////////////////////////////////
ACTION_IF FILE_EXISTS "worldmap/map_mods_links_patch.tbl" BEGIN
  COPY "worldmap/map_mods_links_patch.tbl" "worldmap/map_mods_links_patch.tbl"
    LPM apply_link_patches
  BUT_ONLY
END
*/


////////////////////////////////////////////
//Translations
////////////////////////////////////////////
ACTION_IF FILE_EXISTS "worldmap/map_mods_trans.tra" BEGIN
  COPY "worldmap/map_mods_trans.tra" "worldmap/map_mods_trans.tra"
    LPF fix_table END
    LPM read_trans_array_from_2da
  BUT_ONLY
END

COPY "%tra_path%/english/worldmap.tra" "%tra_path%/english/worldmap.tra" //To gaurd against incomplete translations
     "%tra_path%/%LANGUAGE%/worldmap.tra" "%tra_path%/%LANGUAGE%/worldmap.tra"
  LPM read_trans_array_from_2da
BUT_ONLY

LAM merge_in_translations
