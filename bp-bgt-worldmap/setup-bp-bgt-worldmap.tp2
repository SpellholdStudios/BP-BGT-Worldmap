BACKUP ~bp-bgt-worldmap/backup~
AUTHOR ~www.shsforums.net/forum/401-worldmap/~
VERSION "v12.0.0"
README "bp-bgt-worldmap/language/%LANGUAGE%/readme.html" "bp-bgt-worldmap/language/english/readme.html"

ALWAYS
  INCLUDE ~bp-bgt-worldmap/lib/remove_triggers_macro.tph~
  INCLUDE ~bp-bgt-worldmap/lib/lib.tpa~
  INCLUDE ~bp-bgt-worldmap/lib/fl#bg1pal.tpa~
  INCLUDE ~%MOD_FOLDER%/lib/a7_tools.tpa~

  OUTER_SET debug_reveal = 0

<<<<<<<< .../fl#inline/newlines
%LNL%
>>>>>>>>

  OUTER_SPRINT tra_path ~bp-bgt-worldmap/language~

  ACTION_DEFINE_ARRAY fl#noconvert BEGIN install-osx install-unix install-win32 END
  ACTION_DEFINE_ARRAY fl#reload BEGIN worldmap END
  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/language~
      default_language = ~english~
      noconvert_array = fl#noconvert
      reload_array = fl#reload
  END
END

LANGUAGE English english ~bp-bgt-worldmap/language/english/worldmap.tra~

LANGUAGE Deutsch german  ~bp-bgt-worldmap/language/english/worldmap.tra~
                         ~bp-bgt-worldmap/language/german/worldmap.tra~
                         ~bp-bgt-worldmap/language/german/install-%WEIDU_OS%.tra~

LANGUAGE Italian italian ~bp-bgt-worldmap/language/english/worldmap.tra~
                         ~bp-bgt-worldmap/language/italian/worldmap.tra~
                         ~bp-bgt-worldmap/language/italian/install-%WEIDU_OS%.tra~

LANGUAGE Polish  polish  ~bp-bgt-worldmap/language/english/worldmap.tra~
                         ~bp-bgt-worldmap/language/polish/worldmap.tra~

LANGUAGE Spanish spanish ~bp-bgt-worldmap/language/english/worldmap.tra~
                         ~bp-bgt-worldmap/language/spanish/worldmap.tra~
                         ~bp-bgt-worldmap/language/spanish/install-%WEIDU_OS%.tra~

LANGUAGE French  french  ~bp-bgt-worldmap/language/english/worldmap.tra~
                         ~bp-bgt-worldmap/language/french/worldmap.tra~
                         ~bp-bgt-worldmap/language/french/install-%WEIDU_OS%.tra~

LANGUAGE Russian russian ~bp-bgt-worldmap/language/english/worldmap.tra~
                         ~bp-bgt-worldmap/language/russian/worldmap.tra~

LANGUAGE ~Simplified Chinese~ ~schinese~ ~bp-bgt-worldmap/language/english/worldmap.tra~
                                         ~bp-bgt-worldmap/language/schinese/worldmap.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Part 1: New Clothmap-style map                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\


BEGIN @1000
DESIGNATED 0 LABEL ~worldmap-main~
REQUIRE_PREDICATE ENGINE_IS ~tob bgee bg2ee~ ~This mod cannot be installed on this game~
//REQUIRE_PREDICATE "%WEIDU_OS%" STRING_COMPARE_CASE osx OR FILE_EXISTS ~bp-bgt-worldmap/bin/osx/mosunpack~ ~This mod is not available for your platform~

COPY_EXISTING sw1h01.itm ~override/bp-bgt_wm6.wm6~ // flag to say we've installed wm6 in our game

// ==============================
// Part 1.1: Travel Times and Area Visibility, also input construction (areas, links and translations etc) for build_worldmap
// ==============================

INCLUDE ~bp-bgt-worldmap/lib/load_tables.tpa~

// ==============================
// Part 1.2: Worldmap
// ==============================

ACTION_IF ENGINE_IS ~tob~ BEGIN
  COPY ~bp-bgt-worldmap/bam/bpbgtmap.bam~ override
END ELSE BEGIN
  COPY ~bp-bgt-worldmap/bam/ee/bpbgtmap.bam~ override
    LPF UPDATE_PVRZ_INDICES
      RET
        original_base_index
        new_base_index
    END
	PATCH_PRINT ~original_base_index: %original_base_index%; new_base_index: %new_base_index%~
  ACTION_IF original_base_index >= 0 AND new_base_index >= 0 BEGIN
    ACTION_BASH_FOR ~bp-bgt-worldmap/bam/ee~ ~.+\.pvrz~ BEGIN
      LAF INSTALL_PVRZ
        INT_VAR
          original_base_index
          new_base_index
        STR_VAR
          source_file = EVAL ~%BASH_FOR_FILESPEC%~
      END
    END
  END ELSE FAIL ~Couldn't install PVRZ files~
END

COPY_EXISTING worldmap.wmp override
  READ_LONG    0xc       mo
  WRITE_ASCII  mo        WORLDMAP #8
  READ_LONG    mo + 0x20 na
  READ_LONG    mo + 0x24 ao
  READ_LONG    mo + 0x28 lo
  READ_LONG    mo + 0x2c nl
  WRITE_ASCII  mo + 0x30 BPBGTMAP #8
  FOR (i = 0; i < nl; ++i) BEGIN
    DELETE_BYTES lo 0xd8
  END
  FOR (i = 0; i < na; ++i) BEGIN
    DELETE_BYTES ao 0xf0
    lo -= 0xf0
  END
  WRITE_LONG mo + 0x20 0
  WRITE_LONG mo + 0x24 ao
  WRITE_LONG mo + 0x28 lo
  WRITE_LONG mo + 0x2c 0
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME xnewarea.2da BEGIN
  COPY_EXISTING xnewarea.2da override
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~.*ar3000.*~ ~1~
  IF ~ar3000~
  BUT_ONLY
END

INCLUDE ~bp-bgt-worldmap/lib/build_worldmap.tpa~

// ====================================================
// Part 1.3: Choose the worldmap size
// ====================================================

INCLUDE ~bp-bgt-worldmap/lib/map_size.tpa~




/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Part 2: Worldmap for Throne of Bhaal             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\


// ====================================================
// Part 2.1: Use new worldmap for Throne of Bhaal
// ====================================================

BEGIN @4001
DESIGNATED 1 LABEL ~worldmap-replace-tob-map~
SUBCOMPONENT @4000
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME bp-bgt_wm6.wm6 @900
REQUIRE_PREDICATE !GAME_IS ~tutu tutu_totsc bgee eet~ ~This component is not available for this game~

COPY_EXISTING ~worldmap.wmp~ ~override/worldm25.wmp~
  GET_OFFSET_ARRAY areas WMP_AREAS
  PHP_EACH areas AS _ => off BEGIN
    READ_ASCII off resref
    PATCH_MATCH "%resref%" WITH
      "AR0700"
      "AR0400"
      "AR0300"
      "AR0500"
      "AR0800"
      "AR0900"
      "AR1000"
      "AR1100"
      "AR1400"
      "AR1300"
      "AR1200"
      "AR0020"
      "AR1404"
      "AR1304"
      "AR2000"
      "AR1900"
      "AR1700"
      "AR2500"
      "AR2600"
      "AR1800"
      "AR1600"
      "AR2100"
      "AR1500"
      "AR2300"
      "AR2800"
      BEGIN
        WRITE_LONG off + 0x30 (THIS BAND BNOT BIT1) BAND BNOT BIT2 // Remove 'Reveal from linked area' and 'Can be visited'
      END
      DEFAULT
    END
  END


// ====================================================
// Part 2.2: Clothmap-style map for Throne of Bhaal
// ====================================================

BEGIN @4002
DESIGNATED 2 LABEL ~worldmap-original-tob-map~
SUBCOMPONENT @4000
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME bp-bgt_wm6.wm6 @900
REQUIRE_PREDICATE !GAME_IS ~tutu tutu_totsc bgee eet~ ~This component is not available for this game~

LAF unpack_worldmap STR_VAR map_res = tobmap72 dest_res = tobmap72 END




/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Part 3: Convert old Savegames                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\


BEGIN @5000  //If you want to uninstall this component, you have to make a backup of your savegame folder manually!
DESIGNATED 3 LABEL ~worldmap-update-savegame~
REQUIRE_PREDICATE !ENGINE_IS ~bgee bg2ee~ ~This component is not available for EE-type games~ // It can be ported, however
REQUIRE_PREDICATE ENGINE_IS ~tob~ ~~
NO_LOG_RECORD

MKDIR save mpsave

ACTION_FOR_EACH dir IN save mpsave BEGIN
  ACTION_CLEAR_ARRAY dir_array
  GET_DIRECTORY_ARRAY dir_array "%dir%" ~[0-9]+-.*~

  OUTER_SET wmp = 0

  ACTION_PHP_EACH dir_array AS int => dir BEGIN
    ACTION_FOR_EACH worldmap IN worldmap.wmp worldm25.wmp BEGIN
      ACTION_IF FILE_EXISTS ~%dir%/%worldmap%~ BEGIN
/////////////////////////////////////////
//Store old flag and areanames in a table
        COPY + ~%dir%/%worldmap%~ ~%dir%~
          READ_LONG 0xc       mo
          READ_LONG mo + 0x20 na
          READ_LONG mo + 0x24 ao
          FOR (i = 0; i < na; ++i) BEGIN
            READ_ASCII ao + i * 0xf0        old_area
            READ_LONG  ao + i * 0xf0 + 0x30 old_flag
            TO_LOWER old_area
            SET $flags_array("%wmp%" "%old_area%") = old_flag
          END

///////////////////////
//New savegame wmp file
        COPY_EXISTING + ~%worldmap%~ ~%dir%~
          READ_LONG 0xc       mo
          READ_LONG mo + 0x20 na
          READ_LONG mo + 0x24 ao
          FOR (i = 0; i < na; ++i) BEGIN
            READ_ASCII ao + i * 0xf0 new_area
            TO_LOWER new_area
            PATCH_IF VARIABLE_IS_SET $flags_array("%wmp%" "%new_area%") BEGIN
              WRITE_LONG ao + i * 0xf0 + 0x30 $flags_array("%wmp%" "%new_area%")
            END
          END
        OUTER_SET ++wmp
      END
    END
  END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Part 4: Larger worldmap UI                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
BEGIN ~BG2 style~  //Larger worldmap screen window for the worldmap menu.ui
DESIGNATED 4 LABEL ~worldmap-fullscreen-bgee~
REQUIRE_PREDICATE ENGINE_IS ~bgee bg2ee~ ~This component is not available for non EE-type games~ // It can be ported, however
SUBCOMPONENT ~Larger worldmap screen window for the worldmap menu.ui~
INCLUDE ~%MOD_FOLDER%/lib/ACuimenulib.tph~
OUTER_SPRINT menufolder ~%MOD_FOLDER%/UI/BG2~
INCLUDE ~%MOD_FOLDER%/lib/install_ui.tpa~
//COPY ~%menufolder%~ ~override~


BEGIN ~Dragonspear style~  //Larger worldmap screen window for the worldmap menu.ui
DESIGNATED 5 LABEL ~worldmap-fullscreen-dragonspear-ui~
REQUIRE_PREDICATE ENGINE_IS ~bgee bg2ee~ ~This component is not available for non EE-type games~ // It can be ported, however
SUBCOMPONENT ~Larger worldmap screen window for the worldmap menu.ui~
INCLUDE ~%MOD_FOLDER%/lib/ACuimenulib.tph~
OUTER_SPRINT menufolder ~%MOD_FOLDER%/UI/Dragonspear~
INCLUDE ~%MOD_FOLDER%/lib/install_ui.tpa~
COPY ~%menufolder%~ ~override~
